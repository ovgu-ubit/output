(
  $cfg := params.cfg;
  $tags := $cfg.affiliation_tags;
  $tagHit := function($s) {
    $count(
      $filter($tags, function($t) {
        $contains($lowercase($s), $lowercase($t))
      })
    ) > 0
    };
  /* afid -> affilname lookup map */
  $affById := $merge(
    $.affiliation.{ ($.afid): $.affilname }
  );
  /* alle Affiliations (IDs), deren Name zu Tags passt */
  $hitAfids := $.affiliation[$tagHit(affilname)].afid;
  {
      "title": $."dc:title",
      "doi": $."prism:doi",
      "authors_inst": $.author[
          $count(
                $filter($.afid."$", function($id) { $id in $hitAfids })
            ) > 0
      ].{
            "first_name": $."given-name",
            "last_name": surname,
            "orcid": ORCID,
            "affiliation": $join($distinct(
                $map(
                    $.afid."$"[$ in $hitAfids],
                    function($id) { $lookup($affById, $id) }
                    )
                ),"; ")
        }[],
      "authors": $join($.author.(surname? surname & ", " & $."given-name" : authname), "; "),
      "greater_entity": {
        "label": $."prism:publicationName",
        "identifiers": $append($append($."prism:issn".{
            "type": "issn",
            "value": $.$substring(0,4) & "-" & $.$substring(4,8)
        }, $."prism:eIssn".{
            "type": "issn",
            "value": $.$substring(0,4) & "-" & $.$substring(4,8)
        }), $."prism:isbn".{
            "type": "isbn",
            "value": $
        })
      },
      "pub_date": $."prism:coverDate",
      "pub_type": $.subtypeDescription,
      "status": 1,
      "volume": $."prism:volume",
      "issue" : $."prism:issueIdentifier",
      "article_number": $."article_number",
      "first_page" : (
           $range := $."prism:pageRange" ? $match($."prism:pageRange", /^.+-.+$/) : null;
           $range ? (
            $split := $split($."prism:pageRange","-");
            $split[0]
            ) : undefined;
      ),
      "last_page" : (
           $range := $."prism:pageRange" ? $match($."prism:pageRange", /^.+-.+$/) : null;
           $range ? (
            $split := $split($."prism:pageRange","-");
            $split[1]
            ) : undefined;
      ),
      "page_count": (
          $range := $."prism:pageRange" ? $match($."prism:pageRange", /^(\d+)\s*-\s*(\d+)$/) : null;
          $range ? (
            $split := $split($."prism:pageRange","-");
            $number($split[1]) - $number($split[0]) + 1;
            ) : undefined;
      )
  }
)