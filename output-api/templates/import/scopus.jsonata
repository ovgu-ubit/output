(
  $cfg := params.cfg;
  $tags := $cfg.affiliation_tags;
  {
      "title": $."dc:title",
      "doi": $."prism:doi",
      "authors_inst": $.authorships[
        (
        $hits := $filter(institutions, function($aff) {
          $contains($aff.id, $openalex_id)
        });
        $count($hits) > 0
      )
          ].{
          "first_name": $join($filter($split(author.display_name, " "), function($v, $i) { $i < $count($split(author.display_name, " ")) - 1 })," "),
          "last_name": $split(author.display_name, " ")[-1],
          "orcid": $split($string(author.orcid), "/")[-1],
          "corresponding": is_corresponding,
          "affiliation":  $join(raw_affiliation_strings, "; ")
          }[],
      "authors": $join($.author.(surname? surname & ", " & $."given-name" : authname), "; "),
      "greater_entity": {
        "label": $.primary_location.source.display_name,
        "identifiers": $.primary_location.source.issn ? $.primary_location.source.issn[].{
          "type" : "issn",
          "value" : $
        } : undefined
      },
      "publisher": {
        "label": $.primary_location.source.host_organization_name
      },
      "pub_date": $.publication_date ?? $.publication_year&"-01-01",
      "language": $.language,
      "link": $.primary_location.landing_page_url,
      "funder": $.grants[].{
          "label": funder_display_name
      },
      "pub_type": (
          $type := $.type;
          $type = "article" and $.primary_location.source.type = "conference" ? "conference proceedings" : $type
          ),
      "license": $.license,
      "status": 1,
      "volume": $.biblio.volume,
      "issue" : $.biblio.issue,
      "first_page" : $.biblio.first_page,
      "last_page" : $.biblio.last_page,
      "page_count": (
          $start := $match($string($.biblio.first_page), /^(\d+)$/);
          $end := $match($string($.biblio.last_page), /^(\d+)$/);
          $end and $start ? $number($end.groups[0]) - $number($start.groups[0]) + 1 : undefined
          ),
      "cost_approach": (
          $elem := $.apc_paid ? $.apc_paid : $.apc_list;
          $elem.currency = "EUR" ? $elem.value : undefined;
      )
  }
)